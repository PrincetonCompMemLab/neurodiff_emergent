// This is a test case from sentence gestalt model

Sentence {
	Ate =Action
}

Ate {
	Std:A Ate:V Ate:P Ate:M
}

Ate:V {
	%80 'ate'
	%20 'consumed'
}

// Std agent used for most actions
Std:A =%80 {
	Busdriver:A =Agent // also comments here ok now
	Teacher:A   =Agent
	PitcherPers:A =Agent
	Schoolgirl:A =Agent
}

// Note: following are re-used across Agent and CoAgent so no state there

Busdriver:A {
	%70 'busdriver'
	%20 'adult'
	%10 'someone'
}

Teacher:A {
	%70 'teacher'
	%20 'adult'
	%10 'someone'
}

PitcherPers:A {
	%70 'pitcherpers'
	%20 'child'
	%10 'someone'
}

Schoolgirl:A {
	%70 'schoolgirl'
	%20 'child'
	%10 'someone'
}

Ate:P ? {
	Busdriver:A {
		%70 Steak:P
		%10 Soup:P
		%10 IceCream:P
		%10 Crackers:P
	}
	Teacher:A {
		%70 Soup:P
		%10 Steak:P
		%10 IceCream:P
		%10 Crackers:P
	}
	PitcherPers:A {
		%25 Soup:P
		%25 Steak:P
		%25 IceCream:P
		%25 Crackers:P
	}
	Schoolgirl:A {
		%25 Soup:P
		%25 Steak:P
		%25 IceCream:P
		%25 Crackers:P
	}
}

Steak:P {
	=Patient    // whatever is chosen is set to the patient role
	%70 'steak'
	%20 'food'
	%10 'something'
}

Soup:P {
	=Patient
	%70 'soup'
	%20 'food'
	%10 'something'
}

IceCream:P {
	=Patient
	%70 'icecream'
	%20 'food'
	%10 'something'
}

Crackers:P {
	=Patient
	%70 'crackers'
	%20 'food'
	%10 'something'
}

Ate:M {
	Ate:Pi
	Ate:AoPc
	Ate:L
	Ate:R
}

Ate:Pi ? {
	Steak:P {
		=Instrument=Knife
		%70 'with' 'knife'
		%20 'with' 'utensil'
		%10 'with' 'something'
	}
	Soup:P || IceCream:P { // usual logical operators supported
		=Instrument=Spoon
		%70 'with' 'spoon'
		%20 'with' 'utensil'
		%10 'with' 'something'
	}
	Crackers:P {
		=Instrument=Finger
		%80 'with' 'finger'
		%20 'with' 'something'
	}
}

Ate:AoPc ? {
	Steak:P || IceCream:P { Ate:Ao }
	Soup:P  || Crackers:P { Ate:Pc }
}

Ate:Ao ? {
	Busdriver:A { 'with' Teacher:A =CoAgent=Teacher }
	Teacher:A { 'with' Busdriver:A =CoAgent=Busdriver }
	Schoolgirl:A { 'with' PitcherPers:A =CoAgent=PitcherPers }
	PitcherPers:A { 'with' Schoolgirl:A =CoAgent=Schoolgirl }
}

Ate:Pc ? {
	Soup:P {
		=CoPatient=Crackers
		%80 'with' 'crackers'
		%20 'with' 'something'
	}
	Crackers:P {
		=CoPatient=Jelly
		%80 'with' 'jelly'
		%20 'with' 'something'
	}
}

Ate:L ? {
	Steak:P || Soup:P || Crackers:P {	'in' 'kitchen' =Location=Kitchen }
	IceCream:P {	'in' 'park' =Location=Park }
}

Ate:R ? {
	(Busdriver:A || PitcherPers:A) && (Steak:P || Soup:P || Crackers:P) { 'with' 'gusto' =Adverb=Gusto }  // some pretty complex items are possible
	(Teacher:A || Schoolgirl:A) && (Steak:P || Soup:P || Crackers:P) { 'with' 'daintiness' =Adverb=Daintiness }
	IceCream:P {	'with' 'pleasure' =Adverb=Pleasure }
}


